config { 
    type: "view",
    dependencies: ["type_and_sub_repo_not_null_assertion"]
}

WITH 
    authors_per_project AS (
    SELECT 
        sub_repo,
        type,       
        author,
        commit_hash
    FROM
        ${ref("cleaned")}
    WHERE
        sub_repo IS NOT NULL
        AND type IS NOT NULL
    GROUP BY
    1,
    2,
    3,
    4
    ),
    last_commit_per_project AS (
        SELECT
            sub_repo,
            MAX(author_date) AS author_date
        FROM
            ${ref("cleaned")}
        GROUP BY 
        1
    ),
    last_commits_per_folder AS (
        SELECT 
            a.sub_repo, 
            a.type, 
            b.author_date AS author_date,
            a.author,
            COUNT(commit_hash) AS commits
        FROM
            authors_per_project a
        LEFT JOIN
            last_commit_per_project b
        USING (sub_repo)
        GROUP BY
            1,
            2,
            3,
            4
    )
SELECT 
    sub_repo,
    type,
    author_date,
    ARRAY_AGG(STRUCT(author AS name, commits) ORDER BY commits DESC) AS authors,
    '${dataform.projectConfig.vars.logicalDate}' AS job_logical_date,
    '${dataform.projectConfig.vars.jobDescription}' AS job_description
FROM last_commits_per_folder
GROUP BY
    1,
    2,
    3
ORDER BY
    3 DESC